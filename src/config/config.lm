##### config for login mode
# only login mode need PATH, other mode will retrieve it from parent shell.
if IS_LOGIN {
    set PATH = '/usr/local/bin:/usr/bin:/bin'
}

##### config for interactive mode
if IS_INTERACTIVE {
    ### ---appearance---
    let logo = '
    ⚡┓
      ┃ ┓┏┏┳┓┏┓
      ┗┛┗┻┛┗┗┗ '
    print logo.green().faint() 'lightweight'.color('skyblue') 'ultimate'.color('olivedrab') 'modern'.color('tomato') 'efficient'.color('mediumpurple')


    ### welcome msg
    # set LUME_WELCOME= 'Welcome to Lumesh!'

    ### prompt
    # MODE: 1=use template; 2=use starship; 0=use default.
    set LUME_PROMPT_SETTINGS = {
        MODE: 1,
        TTL_SECS: 2
    }
    # template could be normal expression or func.
    # avaluable tmplate var: $CWD, $CWD_SHORT, $CFM_TAG, $STRICT_TAG
    set LUME_PROMPT_TEMPLATE = (string.blue('$CWD_SHORT') + '|' + string.green('$CFM_TAG') + '|' + string.blue('$STRICT_TAG') + '❯  '.bold().yellow())
    # if template is lambda/func, it will be evaluated everytime.
    set LUME_PROMPT_TEMPLATE = (dir,ctx) -> {
        string.blue($dir) + ' |'.green().bold() \
        + ($ctx.cfm ? 'CFM'.green() + '|' : '')\
        + ($ctx.strict ? 'S'.yellow() + '| ' : '' ) \
        + (if (fs.exists '.git') {git branch --show-current | .cyan()} else '') \
        + '> '.green().bold()
    }
    # include ~/.config/lumesh/prompt_ka.lm

    ### base theme for syntax: 'one_dark', 'ayu_dark', 'light'
    LUME_THEME = 'ayu_dark'

    ### syntax theme modify
    #LUME_THEME_CONFIG = {
    #    keyword: "\x1b[38;5;170m",
    #    ...
    #}

    ### ---interactive---
    ### default repl mode
    # LUME_VI_MODE = true

    ### default strict mode
    # set LUME_STRICT = true
    ### default cmd_frist_mode
    # set LUME_CFM = true

    ### history file
    # set LUME_HISTORY_FILE = '/tmp/lume_histroy'

    # == completion
    # set LUME_COMPLETION_DIR = '~/.local/share/lumesh/completions'
    set LUME_COMPLETION_CYCLE = false

    ### report compute result type and values. default True.
    # set LUME_PRINT_DIRECT= false


    ### sudo cmd for Alt+s
    set LUME_SUDO_CMD = 'doas'

    ### key bindings
    # NONE:0, SHIFT:2, ALT:4, CTRL:8,
    # ALT_SHIFT:6, CTRL_SHIFT: 10, CTRL_ALT:12, CTRL_ALT_SHIFT:14
    set LUME_HOT_MODIFIER = 4
    set LUME_HOT_KEYS = {
        q: 'exit',
        h: "fs.read ~/.cache/.lume_history | string.lines() | ui.pick('select history:') ?! | exec_str()",
        x: "fs.read ~/.cache/lume_bookmark | string.lines() | ui.pick('select bookmark:') ?! | exec_str()",
        m: 'let cmd := "$CMD_CURRENT";let s = into.str(cmd); if s {s+"\n" >> ~/.cache/lume_bookmark; println "\t[MARKED]"}',
        e: 'let tc=/tmp/lm_cmd; "" >! $tc; vi $tc; let lmcode=fs.read($tc); if len($lmcode) && ui.confirm("Execute it?"){exec_str($lmcode)}',
    }

    ### abbreviations

    set LUME_ABBREVIATIONS = {
        xi: 'doas pacman -S',
        xup: 'doas pacman -Syu',
        xq: 'pacman -Q',
        xs: 'pacman -Ss',
        xr: 'doas pacman -Rs',
    }

    ### alias
    alias cds = sys.cds()
    alias int = into.int()
    alias str = into.str()
    alias each = list.map()
    alias table = into.table()
    alias join = list.join()
    alias open = fs.read()
    alias fls = fs.ls('-l')
    alias fmv = fs.mv()
    alias frm = fs.rm()
    alias fcp = fs.cp()
    alias fhead = fs.head()
    alias ftail = fs.tail()
    alias doc = xdg-open https://lumesh.codeberg.page

    ### ---ai---
    ### default AI Helper settings. following is default.
    # set LUME_AI_CONFIG = {
    #     host: 'localhost:11434',
    #     complete_url: '/completion',
    #     chat_url: '/v1/chat/completions',
    #     complete_max_tokens: 10,
    #     chat_max_tokens: 100,
    #     model: '',
    #     system_prompt: "you're a lumesh shell script helper",
    # }

    ### ---update---
    fn update(){
        println 'checking latest version...'
        let url = 'https://codeberg.org/santo/lumesh/releases/latest'
        let ver = (curl -s $url ?: eprintln 'network err' && exit |  regex.find(r'\d[\d\.]+') )
        ver = ver[found]
        let latest = ($ver.split('.').join('').to_int() ?: 0)
        if $latest > 0 {
          let current = (about.version())
          current = $current.split('.').join('').to_int()
          if $latest > $current {
              print 'Found new version: ' $ver.green()
              if ui.confirm('download [normal edition for linux] now?') {
                  println 'download...'
                  let tmp = /tmp/lume
                  curl -o $tmp `https://codeberg.org/santo/lumesh/releases/download/v${ver}/lume-linux`
                  if (file $tmp | .contains('ELF')){
                      chmod '+x' $tmp
                      let dest = about.bin()
                      if $dest.starts_with('/home'){
                        cp -f $tmp $dest
                      }else{
                        bash -c `type sudo && sudo cp -f $tmp $dest || doas cp -f $tmp $dest`
                      }
                      println 'finished'
                  }else{
                      println 'download fail'
                      rm $tmp
                  }
              } else {
                  println 'you may download it manually:' $url
              }
          } else {
              println 'Already newest'.yellow()
          }
        } else {
          eprintln 'version check failed.'
        }
    }

}
# else {
##### config for script mode
# }

##### config for all mode
PATH = '~/.local/bin:' + $PATH ?.
# IFS affect: 0:never; 2:cmd args; 4:for; 8:string.split; 16:csv; 32:pick; 62:all
set LUME_IFS_MODE=2
# set LUME_MODULES_PATH=~/.local/share/lumesh/mods
# set LUME_MAX_SYNTAX_RECURSION = 100
# set LUME_MAX_RUNTIME_RECURSION = 800
